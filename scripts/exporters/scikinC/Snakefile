### Runs with:
###   snakemake all all --config 'slot="2016-MagUp-data"'

import os
import yaml


# +-------------+
# |    Setup    |
# +-------------+

DebugMode = False

STEPS = ["Rich", "Muon", "GlobalMuonId", "GlobalPID"] 
PARTS = ["Muon", "Pion", "Kaon", "Proton"]

with open ("../../training/config/config.yml") as file:
  PATH = yaml.full_load (file) ["model_dir"]

CCFLAGS = ["-Ofast", "--shared", "-fPIC"]
if DebugMode: CCFLAGS.append ("-DDEBUG")

# +-----------------------+
# |    Rule: GAN MODEL    |
# +-----------------------+

rule ganmodel:
  input: 
    model = os.path.join ( PATH, "latest_models/{step}_{part}_{slot}_latest/saved_generator" ) ,
    tX    = os.path.join ( PATH, "latest_models/{step}_{part}_{slot}_latest/transform_X.pkl" ) ,
    tY    = os.path.join ( PATH, "latest_models/{step}_{part}_{slot}_latest/transform_Y.pkl" )

  output:
    model = temp ( "/tmp/{step}_{part}_{slot}_latest.C"    ) ,
    tX    = temp ( "/tmp/{step}_{part}_{slot}_latest_tX.C" ) ,
    tY    = temp ( "/tmp/{step}_{part}_{slot}_latest_tY.C" )

  shell:
    "scikinC --float_t float {wildcards.step}{wildcards.part}={input.model} > {output.model};"
    "scikinC --float_t float {wildcards.step}{wildcards.part}_tX={input.tX} > {output.tX};"
    "scikinC --float_t float {wildcards.step}{wildcards.part}_tY={input.tY} > {output.tY}"
    
# +--------------------+
# |    Rule: ISMUON    |
# +--------------------+

rule ismuoneff:
  input:
    model = os.path.join ( PATH, "latest_models/isMuon_{part}_{slot}_latest/pipeline.pkl" ) ,

  output:
    model = temp ( "/tmp/isMuon_{part}_{slot}_latest.C" ) ,

  shell:
    "scikinC --float_t float IsMuon{wildcards.part}={input.model} > {output.model}"
    
# +----------------------+
# |    Rule: PIPELINE    |
# +----------------------+

rule pipeline:
  input:
    model  = expand ( "/tmp/{step}_{part}_{slot}_latest.C"    , step = STEPS , part = PARTS , slot = config["slot"] , allow_missing = True ) , 
    tX     = expand ( "/tmp/{step}_{part}_{slot}_latest_tX.C" , step = STEPS , part = PARTS , slot = config["slot"] , allow_missing = True ) , 
    tY     = expand ( "/tmp/{step}_{part}_{slot}_latest_tY.C" , step = STEPS , part = PARTS , slot = config["slot"] , allow_missing = True ) , 
    ismuon = expand ( "/tmp/isMuon_{part}_{slot}_latest.C"    ,                part = PARTS , slot = config["slot"] , allow_missing = True ) , 
    pipe = "../cpipelines/pipeline.C"

  output:
    shared_object = os.path.join ( PATH, "latest_models/CompiledModel_{slot}_latest.so" )

  params:
    flags = CCFLAGS

  shell:
    "gcc {params.flags} -o {output.shared_object} {input}"

# +------------------+
# |    Rule: TEST    |
# +------------------+

rule test:
  input:
    script = "tests/test.py" ,
    shared_object = os.path.join ( PATH, "latest_models/CompiledModel_{slot}_latest.so" )

  output:
    temp ( ".tested_{part}_{slot}_latest" )

  shell:
    "python3 {input.script} -i {input.shared_object} -p {wildcards.part} -s {wildcards.slot} && touch {output}"

# +-----------------+
# |    Rule: ALL    |
# +-----------------+

rule all:
  input:
    expand ( ".tested_{part}_{slot}_latest", part = PARTS, slot = config["slot"] )

  output:
    ".compiled_models"

  shell:
    "touch {output}"
